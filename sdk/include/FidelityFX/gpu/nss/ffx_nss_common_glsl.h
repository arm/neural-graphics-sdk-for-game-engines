/*
 * SPDX-FileCopyrightText: Copyright 2025-2026 Arm Limited and/or its affiliates <open-source-office@arm.com>
 * SPDX-License-Identifier: MIT
 */

#if !defined(FFX_NSS_COMMON_GLSL_H)
#define FFX_NSS_COMMON_GLSL_H

#include "ffx_nss_resources.h"

#if defined(FFX_GPU)

#include "ffx_core.h"

#if QUANTIZED
#define tensor_t    int8_t
#define tensorVec_t int8_t4
#else
#define tensor_t    float
#define tensorVec_t float4
#endif

#if REVERSE_Z
#define INVERTED_DEPTH 1
#endif

// Default quality mode to BALANCE (1) if not set by the shader permutation.
#ifndef NSS_SHADER_QUALITY_MODE
#define NSS_SHADER_QUALITY_MODE 1
#endif

#define NSS_SCALE_PRESET_NONE 0
#define NSS_SCALE_PRESET_X2   1

#ifndef SCALE_PRESET_MODE
#define SCALE_PRESET_MODE NSS_SCALE_PRESET_NONE
#endif

// Scale preset permutation is intentionally limited to exact 2x and general.
// The postprocess shader derives the concrete filter implementation from the
// scale preset and quality mode:
//   NSS_FILTER_MODE = 0: static 2x dense
//   NSS_FILTER_MODE = 1: static 2x sparse
//   NSS_FILTER_MODE = 2: dynamic dense LUT
//   NSS_FILTER_MODE = 3: dynamic sparse LUT
#if SCALE_PRESET_MODE == NSS_SCALE_PRESET_X2
#if NSS_SHADER_QUALITY_MODE == 0
#define NSS_FILTER_MODE 0
#else
#define NSS_FILTER_MODE 1
#endif
#else
#if NSS_SHADER_QUALITY_MODE == 0
#define NSS_FILTER_MODE 2
#else
#define NSS_FILTER_MODE 3
#endif
#endif

// Derive individual feature flags from the quality mode.
//   QUALITY (0):      full-res depth scatter, full-res preprocess, no sparse filter, catmull history.
//   BALANCE (1):      quarter-res depth scatter, half-res preprocess, packed offsets, sparse filter, catmull history.
//   PERFORMANCE (2):  quarter-res depth scatter, half-res preprocess, packed offsets, sparse filter, no catmull.
// BALANCE and PERFORMANCE are identical except NSS_USE_HISTORY_CATMULL (1 vs 0).
#if NSS_SHADER_QUALITY_MODE == 0  // QUALITY(high)
#define NSS_DEPTH_SCATTER_QUARTER_RES_INPUT 0
#define NSS_PREPROCESS_HALF_RES_INPUT       0
#define NSS_V1_HIGH_LUMA_DERIVATIVE         1
#define NSS_V1_MID_LOW_LUMA_DERIVATIVE      0
#define NSS_PACKED_NEAREST_OFFSET_QUAD      0
#define NSS_USE_SPARSE_2X2_FILTER           0
#define NSS_USE_HISTORY_CATMULL             1
#elif NSS_SHADER_QUALITY_MODE == 1  // BALANCE(mid)
#define NSS_DEPTH_SCATTER_QUARTER_RES_INPUT 1
#define NSS_PREPROCESS_HALF_RES_INPUT       1
#define NSS_V1_HIGH_LUMA_DERIVATIVE         0
#define NSS_V1_MID_LOW_LUMA_DERIVATIVE      1
#define NSS_PACKED_NEAREST_OFFSET_QUAD      1
#define NSS_USE_SPARSE_2X2_FILTER           1
#define NSS_USE_HISTORY_CATMULL             1
#elif NSS_SHADER_QUALITY_MODE == 2  // PERFORMANCE(low)
#define NSS_DEPTH_SCATTER_QUARTER_RES_INPUT 1
#define NSS_PREPROCESS_HALF_RES_INPUT       1
#define NSS_V1_HIGH_LUMA_DERIVATIVE         0
#define NSS_V1_MID_LOW_LUMA_DERIVATIVE      1
#define NSS_PACKED_NEAREST_OFFSET_QUAD      1
#define NSS_USE_SPARSE_2X2_FILTER           1
#define NSS_USE_HISTORY_CATMULL             0
#else
#error "Unknown shader quality mode!"
#endif

#define NSS_YCOCG_LUMA_DERIVATIVE (NSS_V1_HIGH_LUMA_DERIVATIVE || NSS_V1_MID_LOW_LUMA_DERIVATIVE)

#ifdef INVERTED_DEPTH
#define NSS_NEAREST_STEP(curr_depth, cand_depth) step(curr_depth, cand_depth)
#define NSS_PLANE_DEPTH(prev_depth, curr_depth)  min(prev_depth, curr_depth)
#else
#define NSS_NEAREST_STEP(curr_depth, cand_depth) step(cand_depth, curr_depth)
#define NSS_PLANE_DEPTH(prev_depth, curr_depth)  max(prev_depth, curr_depth)
#endif

///////////////////////////////////////////////
// declare CBs and CB accessors
///////////////////////////////////////////////
#if defined(NSS_BIND_CB_NSS)
layout(set = 0, binding = NSS_BIND_CB_NSS, std140) uniform cbNSS_t
{
    // ─────────────── 32bit precision objects ───────────────
    float4 _DeviceToViewDepth;  //  16 B
    float4 _JitterOffset;       //  16 B (.xy = pixels, .zw = uvs)
    float4 _JitterOffsetTm1;    //  16 B (.xy = pixels, .zw = uvs)
    float4 _ScaleFactor;        //  16 B (.xy = scale, .zw = inv scale)

    int32_t2 _OutputDims;  //   8 B
    int32_t2 _InputDims;   //   8 B

    float2 _InvOutputDims;  //   8 B
    float2 _InvInputDims;   //   8 B

    int32_t2 _DepthTm1Size;     //   8 B
    float2   _InvDepthTm1Size;  //   8 B

    int32_t2 _InputTensorSize;     //   8 B
    float2   _InputTensorSizeRcp;  //   8 B

    int32_t2 _KpnDimension;       //   8 B
    float2   _MotionVectorScale;  //   8 B

    float2 _PaddingScale;               //   8 B  (input / padded_preprocess)
    float  _DepthClipRequiredSepScale;  //   4 B
    float  _DepthClipPower;             //   4 B

    float2  _KpnScale;         //   8 B  (kpnDims / paddedDims)
    int32_t _DebugViewMode;    //   4 B  debug view mode: 0=all tiles, 1-16=single tile fullscreen
    float   _NotHistoryReset;  //   4 B

    float2   _Exposure;     // 8 B  .x = exposure, .y = 1/exp
    int32_t2 _IndexModulo;  // 8 B  reduced output numerator, xy order

    int32_t2 _ReducedInputModulo;  // 8 B reduced input denominator, xy order
    int32_t2 _LutOffset;           // 8 B jitter tile offset, xy order
}
cbNSS;

//--------------------------------------------------------------
// cbNSS accessor helpers.
//--------------------------------------------------------------
// Camera / projection
float4 DeviceToViewDepth()
{
    return cbNSS._DeviceToViewDepth;
}

float4 JitterOffset()
{
    return cbNSS._JitterOffset;
}

float4 JitterOffsetTm1()
{
    return cbNSS._JitterOffsetTm1;
}

// Temporal / exposure
half2 Exposure()
{
    return half2(cbNSS._Exposure);
}

half NotHistoryReset()
{
    return half(cbNSS._NotHistoryReset);
}

float2 MotionVectorScale()
{
    return cbNSS._MotionVectorScale;
}

// Debug view
int32_t DebugViewMode()
{
    return cbNSS._DebugViewMode;
}

int32_t2 ReducedInputModulo()
{
    return cbNSS._ReducedInputModulo;
}

// KPN / postprocess (prototype names that alias input dims)
int32_t2 IndexModulo()
{
    return cbNSS._IndexModulo;
}

int32_t2 LutOffset()
{
    return cbNSS._LutOffset;
}

int32_t2 KpnDims()
{
    return cbNSS._KpnDimension;
}

float2 KpnScale()
{
    return cbNSS._KpnScale;
}

#endif  // #if defined(NSS_BIND_CB_NSS)

//--------------------------------------------------------------
// GPU-side debug view mode constants (matches NssDebugViewMode in ffx_nss_private.h)
// 4x4 tile grid; only NSS_DEBUG_VIEW_MODE_ALL is referenced by the shader, the rest
// document the single-tile fullscreen mode -> tile mapping.
//--------------------------------------------------------------
#define NSS_DEBUG_VIEW_MODE_ALL                      (0)   // 4x4 overview
#define NSS_DEBUG_VIEW_MODE_HISTORY                  (1)   // row0,col0 history_color
#define NSS_DEBUG_VIEW_MODE_DEPTH                    (2)   // row0,col1 input_depth
#define NSS_DEBUG_VIEW_MODE_RECONSTRUCTED_PREV_DEPTH (3)   // row0,col2 prev_depth
#define NSS_DEBUG_VIEW_MODE_NEAREST_OFFSET           (4)   // row0,col3 nearest_offset
#define NSS_DEBUG_VIEW_MODE_LOW_RES_COLOR            (5)   // row1,col0 low_res_color (texture)
#define NSS_DEBUG_VIEW_MODE_MOTION_VECTOR            (6)   // row1,col1 motion_vector (texture)
#define NSS_DEBUG_VIEW_MODE_LUMA_DERIV_TM1           (7)   // row1,col2 luma_deriv_tm1
#define NSS_DEBUG_VIEW_MODE_TEMPORAL_FEEDBACK        (8)   // row1,col3 temporal_feedback (texture)
#define NSS_DEBUG_VIEW_MODE_LR_WARPED_HISTORY        (9)   // row2,col0 lr_warped_history (tensor)
#define NSS_DEBUG_VIEW_MODE_DISOCCLUSION_MASK        (10)  // row2,col1 disocclusion_mask
#define NSS_DEBUG_VIEW_MODE_LUMA_DERIV_T             (11)  // row2,col2 luma_deriv_t (current-frame SRV)
#define NSS_DEBUG_VIEW_MODE_DEPTH_DILATED            (12)  // row2,col3 depth_dilated
#define NSS_DEBUG_VIEW_MODE_TENSOR_COLOR             (13)  // row3,col0 unjittered_color (tensor)
#define NSS_DEBUG_VIEW_MODE_TENSOR_MOTION_DETECTOR   (14)  // row3,col1 motion_detector (tensor)
#define NSS_DEBUG_VIEW_MODE_TENSOR_LUMA_INSTABILITY  (15)  // row3,col2 luma_instability (tensor)
#define NSS_DEBUG_VIEW_MODE_TENSOR_WARP_FEEDBACK     (16)  // row3,col3 warp_feedback (tensor)

layout(set = 0, binding = 1000) uniform sampler s_PointClamp;
layout(set = 0, binding = 1001) uniform sampler s_LinearClamp;

//=========================================================================
// Common Resources for all passes
//=========================================================================

//-------------------------------------------------------------------------
// Input: color-jittered
//-------------------------------------------------------------------------
#if defined(NSS_BIND_SRV_INPUT_COLOR_JITTERED)
layout(set = 0, binding = NSS_BIND_SRV_INPUT_COLOR_JITTERED) uniform mediump texture2D r_input_color_jittered;
#define _ColourTex sampler2D(r_input_color_jittered, s_LinearClamp)
#endif

//-------------------------------------------------------------------------
// Input: motion-vectors
//-------------------------------------------------------------------------
#if defined(NSS_BIND_SRV_INPUT_MOTION_VECTORS)
layout(set = 0, binding = NSS_BIND_SRV_INPUT_MOTION_VECTORS) uniform mediump texture2D r_input_motion_vectors;
#define _MotionTex sampler2D(r_input_motion_vectors, s_LinearClamp)
#endif

//-------------------------------------------------------------------------
// Input: Prev-upscaled-color
//-------------------------------------------------------------------------
#if defined(NSS_BIND_SRV_HISTORY_UPSCALED_COLOR)
layout(set = 0, binding = NSS_BIND_SRV_HISTORY_UPSCALED_COLOR) uniform mediump texture2D r_prev_upscaled_color;
#define _HistoryTex sampler2D(r_prev_upscaled_color, s_LinearClamp)
#endif

//-------------------------------------------------------------------------
// Input: Input-depth (used by depth-scatter and preprocess passes)
//-------------------------------------------------------------------------
#if defined(NSS_BIND_SRV_INPUT_DEPTH)
layout(set = 0, binding = NSS_BIND_SRV_INPUT_DEPTH) uniform highp texture2D r_input_depth;
#define _DepthTex sampler2D(r_input_depth, s_PointClamp)

float2 GatherQuadUvFromTL(int32_t2 base, float2 inv_size)
{
    // Bias inside the intended 2x2 footprint so gather does not sit exactly on
    // a texel-center tie. GLSL gather is swizzled below into TL, TR, BL, BR.
    return (float2(base) + float2(0.75)) * inv_size;
}

float4 GatherDepthQuadTLBR(int32_t2 base, float2 inv_size)
{
    return textureGather(_DepthTex, GatherQuadUvFromTL(base, inv_size), 0).wzxy;
}

#endif

//-------------------------------------------------------------------------
// Output: Reconstructed prev-depth (depth scatter write target) — R32_UINT
//         Shared by depth-scatter (write) and preprocess (read via SRV alias)
//-------------------------------------------------------------------------
#if defined(NSS_BIND_UAV_DEPTH_TM1)
layout(set = 0, binding = NSS_BIND_UAV_DEPTH_TM1, r32ui) coherent uniform uimage2D rw_reconstructed_prev_depth;
#endif

#define FLT_MIN            1.175494351e-38
#define MEDIUMP_FLT_MAX    65504.f
#define MEDIUMP_FLT_MIN    0.00006103515625
#define FLT_EPS            MEDIUMP_FLT_MIN
#define SaturateMediump(x) min(x, MEDIUMP_FLT_MAX)

// clamp HDR value within a safe range
vec3 SafeHDR(vec3 c)
{
    return vec3(clamp(c, 0.f, MEDIUMP_FLT_MAX));
}

// optimised bicubic catmull rom implementation adapted from TAAStandalone.usf in UE4
void Bicubic2DCatmullRom(in vec2 uv, in vec2 size, in vec2 invSize, out vec2 samples[3], out vec2 weights[3])
{
    uv *= size;

    vec2 tc = floor(uv - 0.5) + 0.5;
    vec2 f  = uv - tc;
    vec2 f2 = f * f;
    vec2 f3 = f2 * f;

    vec2 w0 = f2 - 0.5 * (f3 + f);
    vec2 w1 = 1.5 * f3 - 2.5 * f2 + 1.f;
    vec2 w3 = 0.5 * (f3 - f2);
    vec2 w2 = 1.f - w0 - w1 - w3;

    weights[0] = w0;
    weights[1] = w1 + w2;
    weights[2] = w3;

    samples[0] = tc - 1.f;
    samples[1] = tc + w2 / weights[1];
    samples[2] = tc + 2.f;

    samples[0] *= invSize;
    samples[1] *= invSize;
    samples[2] *= invSize;
}

#define BICUBIC_CATMULL_ROM_SAMPLES 5U

struct CatmullRomSamples
{
    // constant number of samples (BICUBIC_CATMULL_ROM_SAMPLES)
    uint Count;

    // bilinear sampling UV coordinates of the samples
    vec2 UV[BICUBIC_CATMULL_ROM_SAMPLES];

    // weights of the samples
    float Weight[BICUBIC_CATMULL_ROM_SAMPLES];

    // final multiplier (it is faster to multiply 3 RGB values than reweights the 5 weights)
    float FinalMultiplier;
};

CatmullRomSamples GetBicubic2DCatmullRomSamples(vec2 uv, vec2 size, in vec2 invSize)
{
    vec2 weights[3];
    vec2 samples[3];
    Bicubic2DCatmullRom(uv, size, invSize, samples, weights);

    CatmullRomSamples crSamples;
    crSamples.Count = BICUBIC_CATMULL_ROM_SAMPLES;

    // optimized by removing corner samples
    crSamples.UV[0] = vec2(samples[1].x, samples[0].y);
    crSamples.UV[1] = vec2(samples[0].x, samples[1].y);
    crSamples.UV[2] = vec2(samples[1].x, samples[1].y);
    crSamples.UV[3] = vec2(samples[2].x, samples[1].y);
    crSamples.UV[4] = vec2(samples[1].x, samples[2].y);

    crSamples.Weight[0] = weights[1].x * weights[0].y;
    crSamples.Weight[1] = weights[0].x * weights[1].y;
    crSamples.Weight[2] = weights[1].x * weights[1].y;
    crSamples.Weight[3] = weights[2].x * weights[1].y;
    crSamples.Weight[4] = weights[1].x * weights[2].y;

    // reweight after removing the corners
    float cornerWeights;
    cornerWeights = crSamples.Weight[0];
    cornerWeights += crSamples.Weight[1];
    cornerWeights += crSamples.Weight[2];
    cornerWeights += crSamples.Weight[3];
    cornerWeights += crSamples.Weight[4];
    crSamples.FinalMultiplier = 1.f / cornerWeights;

    return crSamples;
}

float sigmoid(float x)
{
    // sigmoid(x) = 1 / (1 + exp(-x))
    return (1.f / (1.f + exp(-x)));
}

vec2 sigmoid(vec2 x)
{
    // sigmoid(x) = 1 / (1 + exp(-x))
    return (1.f / (1.f + exp(-x)));
}

vec3 sigmoid(vec3 x)
{
    // sigmoid(x) = 1 / (1 + exp(-x))
    return (1.f / (1.f + exp(-x)));
}

vec4 sigmoid(vec4 x)
{
    // sigmoid(x) = 1 / (1 + exp(-x))
    return (1.f / (1.f + exp(-x)));
}

vec3 clip_aabb(vec3 minc, vec3 maxc, vec3 history)
{
    vec3  center    = 0.5f * (maxc + minc);
    vec3  extents   = 0.5f * (maxc - minc);
    vec3  offset    = history - center;
    vec3  ts        = abs(extents / (offset + FLT_EPS));
    float t         = clamp(min(min(ts.r, ts.g), ts.b), 0.0, 1.0);
    vec3  rectified = center + offset * t;
    return rectified;
}

vec3 RGBToYCoCg(vec3 Rgb)
{
    vec3 YCoCg;
    YCoCg.y   = Rgb.r - Rgb.b;
    float tmp = Rgb.b + YCoCg.y / 2.0;
    YCoCg.z   = Rgb.g - tmp;
    YCoCg.x   = tmp + YCoCg.z / 2.0;
    return YCoCg;
}

float RGBToY(vec3 Rgb)
{
    return 0.25 * Rgb.r + 0.5 * Rgb.g + 0.25 * Rgb.b;
}

vec3 YCoCgToRGB(vec3 YCoCg)
{
    vec3  Rgb;
    float tmp = YCoCg.x - YCoCg.z / 2.0;
    Rgb.g     = YCoCg.z + tmp;
    Rgb.b     = tmp - YCoCg.y / 2.0;
    Rgb.r     = Rgb.b + YCoCg.y;
    return Rgb;
}

float GetViewSpaceDepth(float fDeviceDepth)
{
    return (cbNSS._DeviceToViewDepth[1] / (fDeviceDepth - cbNSS._DeviceToViewDepth[0]));
}

#define lerp mix

#if FFX_HALF
// --- RCP functions for float16 types ---
half rcp(half x)
{
    return half(1.HF) / x;
}
half2 rcp(half2 x)
{
    return half2(1.HF) / x;
}
half3 rcp(half3 x)
{
    return half3(1.HF) / x;
}
half4 rcp(half4 x)
{
    return half4(1.HF) / x;
}

// --- Saturate functions for float16 types ---
half saturate(half x)
{
    return clamp(x, half(0.HF), half(1.HF));
}
half2 saturate(half2 x)
{
    return clamp(x, half2(0.HF), half2(1.HF));
}
half3 saturate(half3 x)
{
    return clamp(x, half3(0.HF), half3(1.HF));
}
half4 saturate(half4 x)
{
    return clamp(x, half4(0.HF), half4(1.HF));
}
#endif

// --- RCP functions for float32 types ---
float rcp(float x)
{
    return float(1.0f) / x;
}
float2 rcp(float2 x)
{
    return float2(1.0f) / x;
}
float3 rcp(float3 x)
{
    return float3(1.0f) / x;
}
float4 rcp(float4 x)
{
    return float4(1.0f) / x;
}

// --- Saturate functions for float32 types ---
float saturate(float x)
{
    return clamp(x, 0.f, 1.f);
}
float2 saturate(float2 x)
{
    return clamp(x, float2(0.f), float2(1.f));
}
float3 saturate(float3 x)
{
    return clamp(x, float3(0.f), float3(1.f));
}
float4 saturate(float4 x)
{
    return clamp(x, float4(0.f), float4(1.f));
}

#define MAX_FP16      65504.HF
#define EPS           1e-7HF
#define MIN_SUMW_HALF 6.1035156e-5HF

#define TONEMAP_KARIS
// #define TONEMAP_REINHARD

#if defined(TONEMAP_KARIS)
half3 Tonemap(half3 x)
{
    // Karis tonemapper
    // http://graphicrants.blogspot.com/2013/12/tone-mapping.html

    // Clamp internal working value, do not mutate input
    half3 xc = clamp(x, half3(0.HF), half3(MAX_FP16));

    // Compute max channel in FP32 to avoid FP16 Flush‑To‑Zero
    float m = max(max(float(xc.r), float(xc.g)), float(xc.b));

    return half3(float3(xc) * (1.0f / (1.0f + m)));
}

half3 InverseTonemap(half3 x)
{
    // Karis tonemapper inverse
    // http://graphicrants.blogspot.com/2013/12/tone-mapping.html
    x = clamp(x, half3(0.HF), half3(1.HF - EPS));
    return x * rcp(half3(1.HF) - max(max(x.r, x.g), x.b));
}
#elif defined(TONEMAP_REINHARD)
vec3 Tonemap(vec3 colour)
{
    // 1 / (1 + colour)
    return colour / (vec3(1.f) + colour);
}

vec3 TonemapExp(vec3 colour, float exponent)
{
    // 1 / (1 + colour)
    colour = colour * exp(exponent);
    return colour / (vec3(1.f) + colour);
}

vec3 InverseTonemap(vec3 colour)
{
    // 1 / (1 - colour)
    return colour / (vec3(1.f) - colour);
}

vec4 Tonemap(vec4 colour)
{
    // 1 / (1 + colour)
    return colour / (vec4(1.f) + colour);
}

vec4 InverseTonemap(vec4 colour)
{
    // 1 / (1 - colour)
    return colour / (vec4(1.f) - colour);
}

#if FFX_HALF
half3 Tonemap(half3 colour)
{
    // 1 / (1 + colour)
    return colour / (half3(1.f) + colour);
}

half3 TonemapExp(half3 colour, half exponent)
{
    // 1 / (1 + colour)
    colour = colour * exp(exponent);
    return colour / (half3(1.f) + colour);
}

half3 InverseTonemap(half3 colour)
{
    // 1 / (1 - colour)
    return colour / (half3(1.f) - colour);
}

half4 Tonemap(half4 colour)
{
    // 1 / (1 + colour)
    return colour / (half4(1.f) + colour);
}

half4 InverseTonemap(half4 colour)
{
    // 1 / (1 - colour)
    return colour / (half4(1.f) - colour);
}
#endif
#endif

bool IsOnScreen(int32_t2 pos, int32_t2 size)
{
    return all(lessThan(uint32_t2(pos), uint32_t2(size)));
}

half Luminance(half3 rgb)
{
    // ITU-R BT.709: `0.2126 * R + 0.7152 * G + 0.0722 * B`
    return dot(rgb, half3(0.2126, 0.7152, 0.0722));
}

// Quantize/Dequantize
// ──────────────────────────────────────────────────────────────────────────────────────────
// all expect .x = scale, .y = zero point, dequantize expect to receive: .x = rcp(scale)

half Dequantize(half i, half2 quant_params)
{
    return (i - quant_params.y) * quant_params.x;
}

half2 Dequantize(half2 i, half2 quant_params)
{
    return (i - quant_params.y) * quant_params.x;
}

half3 Dequantize(half3 i, half2 quant_params)
{
    return (i - quant_params.y) * quant_params.x;
}

half4 Dequantize(half4 i, half2 quant_params)
{
    return (i - quant_params.y) * quant_params.x;
}

int8_t Quantize(half f, half2 quant_params)
{
    return int8_t(clamp(round(f * quant_params.x + quant_params.y), half(-128.HF), half(127.HF)));
}

int8_t2 Quantize(half2 f, half2 quant_params)
{
    return int8_t2(clamp(round(f * quant_params.x + quant_params.y), half2(-128.HF), half2(127.HF)));
}

int8_t3 Quantize(half3 f, half2 quant_params)
{
    return int8_t3(clamp(round(f * quant_params.x + quant_params.y), half3(-128.HF), half3(127.HF)));
}

int8_t4 Quantize(half4 f, half2 quant_params)
{
    return int8_t4(clamp(round(f * quant_params.x + quant_params.y), half4(-128.HF), half4(127.HF)));
}

half3 SafeColour(half3 x)
{
    return clamp(x, half3(0.HF), half3(MAX_FP16));
}

// Encode/Decode
// ──────────────────────────────────────────────────────────────────────────────────────────
// Note: both encode/decode methods are currently bound to 3x3 windows, they should be
//       expandable in future if needed. The most likely to need this would be the jitter
//       encoding, where 3x3 may not be enough for larger than 3x3 scale factors.

uint8_t EncodeNearestDepthCoord(int32_t2 o)
{
    // o ∈ {-2, -1, 0, 1, 2}² — 5×5 = 25 values, fits in 6 bits (≤ 63 < 255).
    // Matches EncodeNearestDepthCoordUNorm in ffx_nss_preprocess.h.
    o = clamp(o, ivec2(-2), ivec2(2));
    return uint8_t(((o.y + 2) << 3) | (o.x + 2));  // values 0-24
}

int32_t2 DecodeNearestDepthCoord(int32_t code)
{
    int32_t x = int32_t(code & 0x7) - 2;         // bits 0-2
    int32_t y = int32_t((code >> 3) & 0x7) - 2;  // bits 3-5
    return int32_t2(x, y);
}

struct BilinearSamplingData
{
    int32_t2 iOffsets[4];
    float    fWeights[4];
    int32_t2 iBasePos;
};

BilinearSamplingData GetBilinearSamplingData(float2 fUv, int32_t2 iSize)
{
    BilinearSamplingData data;
    float2               fPxSample = (fUv * float2(iSize)) - float2(0.5f, 0.5f);
    data.iBasePos                  = int32_t2(floor(fPxSample));
    float2 fPxFrac                 = fract(fPxSample);

    data.iOffsets[0] = int32_t2(0, 0);
    data.iOffsets[1] = int32_t2(1, 0);
    data.iOffsets[2] = int32_t2(0, 1);
    data.iOffsets[3] = int32_t2(1, 1);

    data.fWeights[0] = (1.f - fPxFrac.x) * (1.f - fPxFrac.y);
    data.fWeights[1] = (fPxFrac.x) * (1.f - fPxFrac.y);
    data.fWeights[2] = (1.f - fPxFrac.x) * (fPxFrac.y);
    data.fWeights[3] = (fPxFrac.x) * (fPxFrac.y);

    return data;
}

#endif  // #if defined(FFX_GPU)

//--------------------------------------------------------------
// Output: debug views UAV (compute path)
//--------------------------------------------------------------
#if defined(NSS_BIND_UAV_DEBUG_VIEWS)
layout(set = 0, binding = NSS_BIND_UAV_DEBUG_VIEWS, r11f_g11f_b10f) uniform mediump image2D rw_debug_views;
void StoreDebugView(int32_t2 iPxPos, float4 v)
{
    imageStore(rw_debug_views, iPxPos, v);
}
int32_t2 GetDebugViewDimensions()
{
    return cbNSS._OutputDims;
}
#endif  // #if defined(NSS_BIND_UAV_DEBUG_VIEWS)

//--------------------------------------------------------------
// Output: debug views render target (fragment path)
//--------------------------------------------------------------
#if defined(NSS_BIND_RENDER_TARGET_DEBUG_VIEWS)
layout(location = NSS_BIND_RENDER_TARGET_DEBUG_VIEWS) out mediump vec4 rw_debug_views;
void StoreDebugView(int32_t2 iPxPos, float4 v)
{
    rw_debug_views = v;
}
int32_t2 GetDebugViewDimensions()
{
    return cbNSS._OutputDims;
}
#endif  // #if defined(NSS_BIND_RENDER_TARGET_DEBUG_VIEWS)

#endif  //!defined(FFX_NSS_COMMON_GLSL_H)
